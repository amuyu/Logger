// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.1'
    }
}

ext {
    minSdkVersion = 15
    targetSdkVersion = 25
    compileSdkVersion = 25
    buildToolsVersion = '25.0.1'
    sourceCompatibilityVersion = JavaVersion.VERSION_1_7
    targetCompatibilityVersion = JavaVersion.VERSION_1_7

    IS_UPLOADING = project.getGradle().startParameter.taskNames.any{it.contains('bintrayUpload')}
}

ext.deps = [
        junit      : 'junit:junit:4.12',
        festassert : 'org.easytesting:fest-assert-core:2.0M10',
        robolectric: 'org.robolectric:robolectric:3.0',
]

allprojects {
    repositories {
        jcenter()
    }

    if (IS_UPLOADING && project.name in ['library']) {
        println project.name
        apply plugin: 'maven'

        gradle.taskGraph.whenReady { taskGraph ->
            taskGraph.getAllTasks().find {
                it.path == ":$project.name:generatePomFileForReleasePublication"
            }.doLast {
                file("build/publications/release/pom-default.xml").delete()
                pom {

                    withXml {
                        def dependenciesNode = asNode().appendNode('dependencies')

                        //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                        configurations.implementation.allDependencies.each {
                            if (it.name == 'unspecified')
                                return
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                            dependencyNode.appendNode('scope', 'runtime')
                        }
                    }

                    project {
                        name "logger"
                        artifactId POM_ARTIFACT_ID
                        packaging 'logger.aar'
                        description POM_DESCRIPTION
                        url POM_URL
                        version VERSION_NAME

                        scm {
                            url POM_SCM_URL
                            connection POM_URL
                            developerConnection POM_URL
                        }

                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                            }
                        }

                        developers {
                            developer {
                                id POM_DEVELOPER_ID
                                email POM_DEVELOPER_EMAIL
                            }
                        }
                    }
                }.writeTo("build/publications/release/pom-default.xml")
            }
        }
    }
}



task clean(type: Delete) {
    delete rootProject.buildDir
}
