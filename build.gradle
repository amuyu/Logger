// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.1'
        classpath 'com.novoda:bintray-release:0.8.1'
    }
}

subprojects {
    repositories {
        mavenCentral()
        google()
        jcenter()
    }
}

ext {
    minSdkVersion = 15
    targetSdkVersion = 28
    compileSdkVersion = 28
    buildToolsVersion = '28.0.3'

    IS_UPLOADING = project.getGradle().startParameter.taskNames.any{it.contains('generatePomFileForReleasePublication')}
}

ext.deps = [
        junit      : 'junit:junit:4.12',
        festassert : 'org.easytesting:fest-assert-core:2.0M10',
        robolectric: 'org.robolectric:robolectric:3.0',
        lint: [
                'core': "com.android.tools.lint:lint:26.4.0",
                'api': "com.android.tools.lint:lint-api:26.4.0",
                'checks': "com.android.tools.lint:lint-checks:26.4.0",
                'tests': "com.android.tools.lint:lint-tests:26.4.0",
        ],
]

group = GROUP
version = VERSION_NAME

allprojects {
    repositories {
        jcenter()
    }

    if (IS_UPLOADING && project.name in ['library']) {
        println project.name
        println GROUP
        println VERSION_NAME
        apply plugin: 'maven'

        gradle.taskGraph.whenReady { taskGraph ->
            taskGraph.getAllTasks().find {
                it.path == ":$project.name:generatePomFileForReleasePublication"
            }.doLast {
                println "delete pom"
                file("build/publications/release/pom-default.xml").delete()
                println "pom"
                pom {


                    project {
                        name "logger"
                        groupId GROUP
                        artifactId POM_ARTIFACT_ID
                        packaging 'aar'
                        description POM_DESCRIPTION
                        url POM_URL
                        version VERSION_NAME

                        scm {
                            url POM_SCM_URL
                            connection POM_URL
                            developerConnection POM_URL
                        }

                        licenses {
                            license {
                                name 'The Apache Software License, Version 2.0'
                            }
                        }

                        developers {
                            developer {
                                id POM_DEVELOPER_ID
                                email POM_DEVELOPER_EMAIL
                            }
                        }
                    }
                }.writeTo("build/publications/release/pom-default.xml")
            }
        }
    }
}



task clean(type: Delete) {
    delete rootProject.buildDir
}
